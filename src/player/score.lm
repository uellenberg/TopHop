inline const rounded_digits = 3;
//Uses end_x from end.lm.

inline const score_x = 0;
inline const score_y = -4;
inline const score_width = 8;
inline const score_height = 1.25;
inline const score_degree = 3/5;

//Create the behavior.
createBehavior!("score", {
    //Store the amount of full games played.
    setVal!("games", 0);
    setMut!("games");

    //Make the full score (full games + percent of the current game) inline.
    setVal!("score", 0);
    setInline!("score");

    //Store the high-score.
    setVal!("high_score", 0);
    setMut!("high_score");

    //Update the games if the player is dead (or the game isn't running) or the game ended.
    setValAction!("games", {
        const game = {
            state = 0;

            selectBehavior!("game_manager", {
                state = getValSelect!("game_manager.game");
            });
        };
        
        const ended = {
            state = 0;

            selectBehavior!("game_manager", {
                state = getValSelect!("game_manager.ended");
            });
        };
    
        if(getVal!("base.death.dead") || !game) {
            state = 0;
        } else if(ended) {
            state = games + 1;
        } else {
            state = games;
        }
    });

    //Update the score to the games + the percent of the game played.
    setValAction!("score", {
        const viewport_x = {
            state = 0;

            selectBehavior!("window", {
                state = getValSelect!("transform.x");
            });
        };

        const games = getVal!("games");
        const percent = viewport_x/end_x;

        //Lock the percent to 0-1.
        const locked_percent = min(1, max(0, percent));

        //Return the games + percent.
        state = games + percent;
        
        //If the game isn't running, just return nothing.
        const game = {
            state = 0;

            selectBehavior!("game_manager", {
                state = getValSelect!("game_manager.game");
            });
        };

        if(!game) {
            state = 0;
        }
    }, -295, true, true);

    //Update the high score to the highest score.
    setValAction!("high_score", {
        state = max(high_score, getVal!("score"));
    });

    behaviorCustom!({
        //Use a y-offset to hide the score when the game isn't actually running.
        inline const score_y_offset = {
            //Figure out if there's a game currently running.
            const game = {
                state = 0;

                selectBehavior!("game_manager", {
                    state = getValSelect!("game_manager.game");
                });
            };
            
            //If the value is 1, then the game is running and we don't need any x-offset, otherwise it isn't and we need one.
            if(game) {
                state = 0;
            } else {
                state = -100;
            }
        };
    
        //Store the score in a global variable.
        export const s_core = round(getVal!("score"), rounded_digits)*1000;
        export const h_score = round(getVal!("high_score"), rounded_digits)*1000;

        //Create points for the scores.
        display label = "${s_core} / ${h_score}";
        display hidden = true;
        display color = c_9;
        display outline = false;
        display size = 2;

        point (.25, -4 + score_y_offset);
        
        //Render the score background outline.
        display color = c_9;
        display fill = 1;
        display stroke = 0;
        display thickness = 0;

        polygon({
            const thickness = .025 * 2;

            state = (score_x, score_y + score_y_offset) + (score_width+thickness)*[(-1/2, 0), (1/2, 0), (1/2, 0), (-1/2, 0)] + (score_height+thickness)*[(0, -1/2), (0, -1/2), (0, 1/2), (0, 1/2)] + [(score_degree, 0), (0, 0), (score_degree, 0), (0, 0)];
        });

        //Render a background for the scores.
        display color = c_6;
        display fill = 1;
        display stroke = 0;
        display thickness = 0;

        polygon({
            state = (score_x, score_y + score_y_offset) + score_width*[(-1/2, 0), (1/2, 0), (1/2, 0), (-1/2, 0)] + score_height*[(0, -1/2), (0, -1/2), (0, 1/2), (0, 1/2)] + [(score_degree, 0), (0, 0), (score_degree, 0), (0, 0)];
        });
    });
});